apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply from: 'version_control.gradle'

Properties VERSIONS = getVersionInfo()

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.uvets.uvets"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode VERSIONS['VERSION_CODE'].toInteger()
        versionName getVersion(VERSIONS)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix ".DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        mock {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"https://private-24263-uvets.apiary-mock.com\""
            buildConfigField "String", "ENV_NAME", "\"MOCK\""
        }
        local {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"http://172.22.236.191:5000\""
            buildConfigField "String", "ENV_NAME", "\"LOCAL\""
        }
        server {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"http://uvetsapi-env.gevzwxdav3.sa-east-1.elasticbeanstalk.com\""
            buildConfigField "String", "ENV_NAME", "\"\""
        }
    }
}

dependencies {
    def room_version = '2.0.0'
    def timber_version = "4.6.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Dependency Injection
    implementation 'org.koin:koin-android:2.0.0-rc-2'
    implementation 'org.koin:koin-android-viewmodel:2.0.0-rc-2'

    // Logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'com.ncapdevi:frag-nav:2.4.0'
    implementation 'com.afollestad.material-dialogs:core:2.0.3'
    implementation 'br.com.concrete:canarinho:2.0.1'
    implementation "com.orhanobut:hawk:2.0.1"
    implementation 'com.github.jkwiecien:EasyImage:3.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'id.zelory:compressor:2.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}
