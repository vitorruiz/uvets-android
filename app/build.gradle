apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.uvets.uvetsandroid"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    androidExtensions {
        experimental = true
    }
    buildTypes {
        debug {
            versionNameSuffix ".DEBUG"
        }
        debugMini {
            initWith debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
            versionNameSuffix ".DEBUG MINI"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        mock {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"https://private-24263-uvets.apiary-mock.com\""
            buildConfigField "String", "ENV_NAME", "\"MOCK\""
        }
        local {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"http://192.168.0.6:5000\""
            buildConfigField "String", "ENV_NAME", "\"LOCAL\""
        }
        server {
            dimension "env"
            buildConfigField "String", "BASE_URL", "\"http://uvetsapi-env.gevzwxdav3.sa-east-1.elasticbeanstalk.com\""
            buildConfigField "String", "ENV_NAME", "\"\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def room_version = '2.0.0'
    def timber_version = "4.6.1"
    def rxjava_version = '2.2.6'
    def rxandroid_version = '2.1.0'
    def retrofit_version = '2.5.0'
    def gson_version = '2.8.5'
    def koin_version = '2.0.0-rc-2'
    def map_version = '16.1.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    implementation "com.google.android.gms:play-services-maps:$map_version"

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-config:16.5.0'
    implementation 'com.google.firebase:firebase-messaging:17.6.0'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Dependency Injection
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // Logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"

    implementation 'com.ncapdevi:frag-nav:3.2.0'
    implementation 'com.afollestad.material-dialogs:core:2.8.1'
    implementation 'com.afollestad.material-dialogs:datetime:2.8.1'
    implementation 'br.com.concrete:canarinho:2.0.1'
    implementation 'com.github.esafirm.android-image-picker:imagepicker:2.0.0'
    implementation 'id.zelory:compressor:2.1.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.airbnb.android:lottie:3.0.0'
    implementation 'com.blankj:utilcodex:1.23.7'
    implementation 'bloder.com:blitz:0.0.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}

apply plugin: 'com.google.gms.google-services'
